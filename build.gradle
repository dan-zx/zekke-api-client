subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'findbugs'

    group 'com.github.danzx.zekke'
    version '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_1_7
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint']

    repositories {
        mavenCentral()
    }

    ext {
        assertjVersion     = '2.8.0'
        jsr305Version      = '3.0.2'
        junitVersion       = '4.12'
        junitParamsVersion = '1.1.0'
    }

    dependencies {
        compile "com.google.code.findbugs:jsr305:${jsr305Version}"

        testCompile "junit:junit:${junitVersion}"
        testCompile "org.assertj:assertj-core:${assertjVersion}"
        testCompile "pl.pragmatists:JUnitParams:${junitParamsVersion}"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.2'
}
